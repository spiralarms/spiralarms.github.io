title: SICP Exercise 2.57
slug: sicp-exercise-2-57

Code
```
:::scheme
#lang racket

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum
          (make-product (multiplier exp)
                        (deriv (multiplicand exp) var))
          (make-product (deriv (multiplier exp) var)
                        (multiplicand exp))))
        ((exponentiation? exp) ; dn/d(exp) = 0
         (let ((u (base exp))
               (n (exponent exp)))
           (make-product (make-product
                          n
                          (make-exponentiation u (make-sum n -1)))
                         (deriv u var))))
        (else
         (error "unknown expression type -- DERIV" exp))))


;;; representing algebraic expressions

(define (variable? x) (symbol? x))

(define (same-variable? v1 v2)
  (and (variable? v1) (variable? v2) (eq? v1 v2)))

(define (=number? exp num)
  (and (number? exp) (= exp num)))

;; sum

(define (sum? x)
  (and (pair? x) (eq? (car x) '+)))

(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list '+ a1 a2))))

(define (addend s) (cadr s))

(define (augend s) ; changed
  (if (> (length s) 3)
      (append (list '+) (cddr s))
      (caddr s)))

;; product

(define (product? x)
  (and (pair? x) (eq? (car x) '*)))

(define (make-product m1 m2)
  (cond ((or (=number? m1 0) (=number? m2 0)) 0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (number? m1) (number? m2)) (* m1 m2))
        (else (list '* m1 m2))))

(define (multiplier p) (cadr p))

(define (multiplicand p) ; changed
  (if (> (length p) 3)
      (append (list '*) (cddr p))
      (caddr p)))

;; exponentiation

(define (exponentiation? x)
  (and (pair? x) (eq? (car x) '**)))

(define (make-exponentiation base exp)
  (cond ((=number? exp 0) 1)
        ((=number? exp 1) base)
        ((=number? base 1) 1)
        ((and (number? base) (number? exp)) (expt base exp))
        (else (list '** base exp))))

(define (base s) (cadr s))

(define (exponent s) (caddr s))


; tests

(augend '(+ x y z t))
(multiplicand '(* x y z t))
(deriv '(* x y (+ x 3)) 'x)
(deriv '(* x x x) 'x)
(deriv '(+ (* x 3) (* 5 (+ x y 2))) 'x)
```

Output
```
:::text
'(+ y z t)
'(* y z t)
'(+ (* x y) (* y (+ x 3)))
'(+ (* x (+ x x)) (* x x))
8
```
