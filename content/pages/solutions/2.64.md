title: SICP Exercise 2.64
slug: sicp-exercise-2-64

Code
```
:::scheme
#lang racket

; set as binary tree 2.63

(define (entry tree) (car tree))

(define (left-branch tree) (cadr tree))

(define (right-branch tree) (caddr tree))

(define (make-tree entry left right)
  (list entry left right))

; converters

(define (tree->list-2 tree)
  (define (copy-to-list tree result-list)
    (if (null? tree)
        result-list
        (copy-to-list (left-branch tree)
                      (cons (entry tree)
                            (copy-to-list (right-branch tree)
                                          result-list)))))
  (copy-to-list tree '()))

; transform ordered list to balanced binary tree

(define (list->tree elements)
  (car (partial-tree elements (length elements))))

(define (partial-tree elts n)
  (if (= n 0)
      (cons '() elts)
      (let ((left-size (quotient n 2)))
        (let ((left-result (partial-tree elts left-size)))
          (let ((left-tree (car left-result))
                (non-left-elts (cdr left-result))
                (right-size (- n (+ left-size 1))))
            (let ((this-entry (car non-left-elts))
                  (right-result (partial-tree (cdr non-left-elts) right-size)))
              (let ((right-tree (car right-result))
                    (remaining-elts (cdr right-result)))
                (cons (make-tree this-entry left-tree right-tree) remaining-elts))))))))

; tests

(list->tree '(1 3 5 7 9 11))
```

Output
```
:::text
'(7 (3 (1 () ()) (5 () ())) (11 (9 () ()) ()))
```

a) `partial-tree` divides the ordered list in two approximately equal halfs
and one entry between them and recursively builds a tree of these parts.

b) $T(n)=2T(n/2)+O(1)=O(n)$
